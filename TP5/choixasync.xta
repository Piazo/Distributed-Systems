/* Probleme du choix distant en asynchrone avec controle de flux */ 

const int ACK = 3;
const int MESG = 5;

chan LRGET, LRPUT;
int  LRVG,  LRVP;

chan RLGET, RLPUT;
int  RLVG,  RLVP;

process site(chan &in, chan &out, int &vin, int &vout) {

int mesg;
state
    idle,
    apres_envoi,
    apres_recoit,
    L0,
    Error,
    L1;
urgent
    idle,
    apres_envoi,
    apres_recoit;
init
    idle;
trans
    L1 -> apres_envoi { sync out!; assign vout:= ACK; },
    L0 -> L1 { guard mesg == MESG; },
    apres_recoit -> Error { guard mesg == ACK; },
    L0 -> idle { guard mesg == ACK; },
    apres_envoi -> L0 { sync in?; assign mesg := vin; },
    idle -> apres_envoi { sync out!; assign vout := MESG; },
    idle -> apres_recoit { sync in?; assign mesg := vin; },
    apres_recoit -> idle { sync out!; assign vout := ACK; };
}

process fifo(const int cap, chan &get, chan &put,  int &vget,  int &vput, int control) {

        int cpt := 0;
        int buf[cap];
        int ixP := 0;
        int ixG := 0;
state
    service,
    erreur;
urgent
    service,
    erreur;
init
    service;
trans
    service -> erreur { guard cpt == cap && control == 1; sync put?; },
    service -> service { guard cpt > 0; sync get!; assign vget:= buf[ixG], 
                cpt:= cpt - 1, ixG:= (ixG == (cap - 1))?0:(ixG + 1); },
    service -> service { guard cpt < cap; sync put?; assign buf[ixP]:= vput, cpt:= cpt + 1, 
                ixP:= (ixP == (cap - 1))?0:(ixP + 1); };
}





FifoLR  := fifo(4,LRGET,LRPUT,LRVG,LRVP, 1);
FifoRL  := fifo(4,RLGET,RLPUT,RLVG,RLVP, 1);

gauche := site(RLGET,LRPUT,RLVG,LRVP);
droit :=  site(LRGET,RLPUT,LRVG,RLVP);



system gauche, FifoLR, FifoRL, droit;